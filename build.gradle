buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
    }
}

plugins {
    id "com.zyxist.chainsaw" version "0.3.1"
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'io.codearte.nexus-staging' version '0.9.0'
}

configure(allprojects) { project ->

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: 'com.bmuschko.nexus'

    group = 'org.ubl'

    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    repositories {
        mavenLocal()
        mavenCentral()
        flatDir {
            dirs "$rootProject.projectDir/lib"
        }
    }

    ext {
        name = 'Systematik Catalogue Builder'
        moduleName = 'org.ubl.scb'
        description = 'An Web Annotation Creation Tool'
        url = 'https://github.com/ub-leipzig/systematik-catalogue-builder'
        inceptionYear = '2017'
        vendor = 'Pandora Ecosystem'
        homepage = 'http://cool.pandora'
        docURL = 'https://github.com/ub-leipzig/systematik-catalogue-builder/wiki'
        license = 'Apache 2'

        apiGuardianVersion = '1.0.0'
        commonsCodecVersion = '1.11'
        commonsIoVersion = '2.6'
        dropwizardVersion = '1.2.0'
        metadataExtractorVersion = '2.11.0'
        jaiImageIoVersion = '1.3.0'
        jacksonVersion = '2.9.4'
        javaxActivationVersion = '1.2.0'
        jettyAlpnVersion = '9.4.8.v20171121'
        jsonLdVersion = '0.11.1'
        junitVersion = '5.1.1'
        ldpClientVersion = '0.2.0-SNAPSHOT'
        logbackVersion = '1.2.3'
        slf4jVersion = "1.7.25"
        commonsRdfVersion = '0.5.0'
        jaxrsVersion = '2.1'
        trellisVersion = '0.5.2'
        trellisAppVersion = '0.7.0-SNAPSHOT'
        xerxesVersion = '2.11.0'

        /* Transitive patched modules */
        commonsLoggingVersion = '1.2'
        findbugsVersion = '3.0.2'
        geronimoAnnotationSpecVersion = '1_2'
        geronimoJmsSpecVersion = '1.1.1'
        hamcrestLibraryVersion = '1.3'
        httpClientOsgiVersion = '4.5.3'
        httpCoreOsgiVersion = '4.4.6'
        javaxAnnotationVersion = '1.3.2'
        jaxbCoreVersion = '2.2.11'
        jenaVersion = '3.6.0'
        jsr305Version = '3.0.2'
        log4jVersion = '1.2.17'
        servicemixXercesVersion = '2.11.0_1'
        xmlApisVersion = '1.4.01'

        jacocoVersion = '0.8.0'
        checkstyleVersion = '8.8'
    }
}

subprojects {

    dependencies {
        testCompile("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
            options.compilerArgs.addAll(["--add-modules", "jdk.incubator.httpclient"])
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    modifyPom {
        //noinspection GroovyAssignabilityCheck
        project {
            //noinspection GroovyAssignabilityCheck
            name name
            //noinspection GroovyAssignabilityCheck
            description description
            url url
            inceptionYear inceptionYear

            organization {
                //noinspection GroovyAssignabilityCheck
                name vendor
                url homepage
            }

            developers {
                developer {
                    id 'christopher-johnson'
                    //noinspection GroovyAssignabilityCheck
                    name 'Christopher Johnson'
                    email 'chjohnson @ (domain of organization url)'
                    organization 'Pan-dora Ecosystem'
                    organizationUrl 'https://pan-dora.github.io/'
                    roles {
                        role 'developer'
                    }
                    timezone '+1'
                }
            }

            scm {
                connection 'scm:git:git://github.com/ub-leipzig/systematik-catalogue-builder.git'
                developerConnection 'scm:git:git@github.com/ub-leipzig/systematik-catalogue-builder.git'
                url 'https://github.com/ub-leipzig/systematik-catalogue-builder/'
                tag 'HEAD'
            }

            licenses {
                license {
                    //noinspection GroovyAssignabilityCheck
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                }
            }
        }
    }

    nexusStaging {
        packageGroup = "cool.pandora"
        stagingProfileId = "4d5f13c386f543"
    }

    nexus {
        sign = true
        repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleVersion
    }

    license {
        include "**/*.java"
        //noinspection GroovyAssignabilityCheck
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        //noinspection GroovyAssignabilityCheck
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    javadoc {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.addStringOption('-module-path', classpath.asPath)
            options.addBooleanOption('html5', true)
        }
        destinationDir = new File(projectDir, "docs/${version}")
        options {
            links 'https://trellis-ldp.github.io/trellis/apidocs/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
        }
    }

}

configurations {
    all {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'javax.annotation' , module: 'jsr250-api'
    }
}

project('image') {
    apply plugin: 'com.zyxist.chainsaw'
    javaModule {
        name = 'org.ubl.image.metadata'
        hacks {
            patches('org.apache.httpcomponents:httpcore-nio', 'org.apache.httpcomponents:httpcore-osgi')
        }
    }

    dependencies {
        compile group: 'commons-codec', name: 'commons-codec', version: commonsCodecVersion
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
        compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
        compile "com.drewnoakes:metadata-extractor:${metadataExtractorVersion}"
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
        testRuntimeClasspath group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        testCompile group: 'org.apiguardian', name: 'apiguardian-api', version: apiGuardianVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    }
}

project('web-anno') {
    apply plugin: 'com.zyxist.chainsaw'

    javaModule.name = 'org.ubl.scb'
    javaModule {
        name = 'org.ubl.scb'
        extraTestModules = ['org.trellisldp.app','org.apache.commons.rdf.jena']
        hacks {
            patches('com.sun.xml.bind:jaxb-impl', 'com.sun.xml.bind:jaxb-core')
            patches('org.slf4j:log4j.over.slf4j', 'log4j:log4j')
            patches('com.google.code.findbugs:jsr305', 'javax.annotation:jsr250-api')
            patches('org.apache.geronimo.specs:geronimo-annotation', 'javax.annotation:javax.annotation-api')
            patches('xerces:xercesImpl', 'org.apache.servicemix.bundles:org.apache.servicemix.bundles.xerces')
            patches('org.apache.httpcomponents:httpcore', 'org.apache.httpcomponents:httpcore-osgi')
            patches('org.apache.httpcomponents:httpcore-nio', 'org.apache.httpcomponents:httpcore-osgi')
            patches('org.apache.jena:jena-shaded-guava', 'org.apache.jena:jena-osgi')
            patches('org.apache.jena:jena-core', 'org.apache.jena:jena-osgi')
            patches('org.apache.jena:jena-arq', 'org.apache.jena:jena-osgi')
            patches('org.apache.geronimo.specs:geronimo-jms_1.1_spec', 'javax.jms:javax.jms-api')
            patches('org.hamcrest:hamcrest-library', 'org.hamcrest:hamcrest-core')
            patches('org.apache.httpcomponents:httpclient-osgi', 'org.apache.httpcomponents:httpclient')
            patches('xml-apis:xml-apis', 'xerces:xercesImpl')
            patches('org.slf4j:jcl.over.slf4j', 'commons-logging:commons-logging')
        }
    }

    dependencies {

        patch "com.sun.xml.bind:jaxb-core:${jaxbCoreVersion}"
        patch "org.hamcrest:hamcrest-library:$hamcrestLibraryVersion"
        patch "log4j:log4j:${log4jVersion}"
        patch "commons-logging:commons-logging:${commonsLoggingVersion}"
        patch "org.apache.httpcomponents:httpclient-osgi:$httpClientOsgiVersion"
        patch "com.google.code.findbugs:jsr305:${findbugsVersion}"
        patch "org.apache.geronimo.specs:geronimo-annotation_1.2_spec:1.0-alpha-1"
        patch "org.apache.servicemix.bundles:org.apache.servicemix.bundles.xerces:${servicemixXercesVersion}"
        patch "org.apache.httpcomponents:httpcore:${httpCoreOsgiVersion}"
        patch "org.apache.httpcomponents:httpcore-nio:${httpCoreOsgiVersion}"
        patch "org.apache.jena:jena-shaded-guava:${jenaVersion}"
        patch "org.apache.jena:jena-core:${jenaVersion}"
        patch "org.apache.jena:jena-arq:${jenaVersion}"
        patch "org.apache.geronimo.specs:geronimo-jms_1.1_spec:${geronimoJmsSpecVersion}"
        patch "xml-apis:xml-apis:${xmlApisVersion}"

        implementation project(':image')
        implementation group: 'commons-io', name: 'commons-io', version: commonsIoVersion
        implementation group: 'cool.pandora', name: 'ldp-client', version: ldpClientVersion
        implementation group: 'com.sun.activation', name: 'javax.activation', version: javaxActivationVersion
        implementation group: 'com.github.jsonld-java', name: 'jsonld-java', version: jsonLdVersion
        implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: httpClientOsgiVersion
        implementation group: 'org.apache.httpcomponents', name: 'httpclient-cache', version: httpClientOsgiVersion
        implementation group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion
        implementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
        implementation group: 'org.trellisldp', name: 'trellis-vocabulary', version: trellisVersion
        implementation "javax.ws.rs:javax.ws.rs-api:${jaxrsVersion}"
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
        implementation group: 'xerces', name: 'xercesImpl', version: xerxesVersion
        runtimeClasspath group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion

        testImplementation group: 'xerces', name: 'xercesImpl', version: xerxesVersion
        testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
        testImplementation("cool.pandora:ldp-client:${ldpClientVersion}")
        testImplementation group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
        testImplementation group: 'org.eclipse.jetty', name: 'jetty-alpn-java-server', version: jettyAlpnVersion
        testImplementation group: 'org.trellisldp', name: 'trellis-app', version: trellisAppVersion
        testImplementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
        testRuntimeClasspath group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    }
}
