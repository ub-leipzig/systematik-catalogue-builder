buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.1'
    }
}

plugins {
    id "com.zyxist.chainsaw" version "0.3.1"
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'io.codearte.nexus-staging' version '0.9.0'
}

ext {
    apiguardianVersion = '1.0.0'
    commonsCodecVersion = '1.11'
    commonsIoVersion = '2.6'
    dropwizardVersion = '1.2.0'
    iiifVocabularyVersion = '0.1.0'
    imageMetadataVersion = '0.1.0'
    metadataExtractorVersion = '2.11.0'
    jaiImageIoVersion = '1.3.0'
    jacksonVersion = '2.9.4'
    javaxActivationVersion = '1.2.0'
    jettyAlpnVersion = '9.4.8.v20171121'
    jsonLdVersion = '0.11.1'
    junitVersion = '5.1.1'
    ldpClientVersion = '0.2.0-SNAPSHOT'
    logbackVersion = '1.2.3'
    slf4jVersion = "1.7.25"
    commonsRdfVersion = '0.5.0'
    jaxrsVersion = '2.1'
    trellisVersion = '0.7.0-SNAPSHOT'
    xerxesVersion = '2.11.0'

    /* Trellis Testing modules */
    activationApiVersion = '1.2.0'
    commonsCollectionsVersion = '4.1'
    commonsLoggingVersion = '1.2'
    findbugsVersion = '3.0.2'
    geronimoAnnotationSpecVersion = '1_2'
    geronimoJmsSpecVersion = '1.1.1'
    hamcrestLibraryVersion = '1.3'
    httpclientVersion = '4.5.3'
    javaxAnnotationVersion = '1.3.2'
    jaxbCoreVersion = '2.2.11'
    jenaArqVersion = '3.8.0-SNAPSHOT'
    jenaRdfConnectionVersion = '3.8.0-SNAPSHOT'
    jenaVersion = '3.7.0'
    jsonldVersion = '0.12.0'
    jsonWebTokenVersion = '0.9.0'
    jsr305Version = '3.0.2'
    log4jVersion = '1.2.17'
    kafkaVersion = '1.0.0_1'
    mustacheVersion = '0.9.5_1'
    servicemixXercesVersion = '2.11.0_1'
    thriftVersion = '0.11.0'
    xmlApisVersion = '1.4.01'

    jacocoVersion = '0.8.1'
    checkstyleVersion = '8.8'

    jacocoTestProjects = ['scb.creator, scb.templates']

}

allprojects { subproj ->

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    group = 'de.ubleipzig'

    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    ext {
        name = 'Systematik Catalogue Builder'
        moduleName = 'de.ubleipzig.scb'
        description = 'An Web Annotation Creation Tool'
        url = 'https://github.com/ub-leipzig/systematik-catalogue-builder'
        inceptionYear = '2017'
        vendor = 'Pandora Ecosystem'
        homepage = 'https://github.com/ub-leipzig/'
        docURL = 'https://github.com/ub-leipzig/systematik-catalogue-builder/wiki'
        license = 'Apache 2'
    }

    repositories {
        mavenCentral()
        flatDir {
            dirs "$rootProject.projectDir/lib"
        }
    }

    junitPlatformTest.jvmArgs("--add-modules", "jdk.incubator.httpclient")

    dependencies {
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
        testImplementation("org.apiguardian:apiguardian-api:$apiguardianVersion")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    }
}

subprojects { subproj ->

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
            options.compilerArgs.addAll(["--add-modules", "jdk.incubator.httpclient"])
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    modifyPom {
        //noinspection GroovyAssignabilityCheck
        project {
            //noinspection GroovyAssignabilityCheck
            name name
            //noinspection GroovyAssignabilityCheck
            description description
            url url
            inceptionYear inceptionYear

            organization {
                //noinspection GroovyAssignabilityCheck
                name vendor
                url homepage
            }

            developers {
                developer {
                    id 'christopher-johnson'
                    //noinspection GroovyAssignabilityCheck
                    name 'Christopher Johnson'
                    email 'chjohnson @ (domain of organization url)'
                    organization 'UB Leipzig'
                    organizationUrl 'https://pan-dora.github.io/'
                    roles {
                        role 'developer'
                    }
                    timezone '+1'
                }
            }

            scm {
                connection 'scm:git:git://github.com/ub-leipzig/systematik-catalogue-builder.git'
                developerConnection 'scm:git:git@github.com/ub-leipzig/systematik-catalogue-builder.git'
                url 'https://github.com/ub-leipzig/systematik-catalogue-builder/'
                tag 'HEAD'
            }

            licenses {
                license {
                    //noinspection GroovyAssignabilityCheck
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                }
            }
        }
    }

    nexusStaging {
        packageGroup = "de.ubleipzig"
        stagingProfileId = "4d5f13c386f543"
    }

    nexus {
        sign = true
        repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    checkstyle {
        configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
        configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
        toolVersion = checkstyleVersion
    }

    license {
        include "**/*.java"
        //noinspection GroovyAssignabilityCheck
        header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
        //noinspection GroovyAssignabilityCheck
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    javadoc {
        inputs.property("moduleName", moduleName)
        doFirst {
            options.addStringOption('-module-path', classpath.asPath)
            options.addBooleanOption('html5', true)
        }
        destinationDir = new File(subproj.buildDir, "docs/${version}")
        options {
            links 'https://trellis-ldp.github.io/trellis/apidocs/'
            links 'https://commons.apache.org/proper/commons-rdf/apidocs/'
        }
    }

}

configure(rootProject) {

    task apidocs(type: Javadoc, dependsOn: getTasksByName('docs', true)) {
        outputs.upToDateWhen { false }
        destinationDir = new File(projectDir, "docs/apidocs")
        title = "SCB Documentation"
        exclude '**/impl/*'
        exclude '**/*Tests.java'
        options {
            memberLevel = JavadocMemberLevel.PUBLIC
            links "https://docs.oracle.com/javase/10/docs/api/"
        }

        source subprojects.collect { project -> project.sourceSets.main.allJava }
        classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
    }

    task jacocoMerge(type: JacocoMerge) {
        subprojects.findAll { it.name in jacocoTestProjects }
                .each { subproj ->
            executionData fileTree(dir: "${subproj.buildDir}/jacoco", include: '*.exec')
            dependsOn subproj.tasks.withType(Test)
        }
    }

    task jacocoRootReport(type: JacocoReport, dependsOn: jacocoMerge) {
        sourceDirectories = files(subprojects
                .findAll { it.name in jacocoTestProjects }
                .sourceSets.main.allJava.srcDirs)
        classDirectories = files(subprojects
                .findAll { it.name in jacocoTestProjects }
                .sourceSets.main.output)
        executionData jacocoMerge.destinationFile
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }
}

